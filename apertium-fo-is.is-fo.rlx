###############################################################################
#
#	This is a partial conversion of the rule file from the IceTagger 
#	part-of-speech tagger for Icelandic by Hrafn Loftsson.
#
###############################################################################

SOFT-DELIMITERS = "<,>" ;
DELIMITERS = "<.>"ri "<:>"ri "<!>"ri "<?>"ri ;

LIST BOS = (>>>) (<s>) (sent) ; 
LIST EOS = (<<<) (</s>) (sent) ; 

LIST N = (n) ;
LIST ProperN = (np) ;
LIST Adj = (adj) ;
LIST Strong = (sta);
LIST Weak = (vei);
LIST Adv = (adv) ;

LIST Prn = (prn) ;
LIST PrnPers = (prn p1) (prn p2) (prn p3) ;
LIST PrnInd = (prn ind);
LIST PrnDem = (prn dem) ;
LIST PrnRefl = (prn ref) ;
LIST PrnItg = (prn itg) ;
LIST PrnPoss = (prn pos) ;

LIST Prep = (pr) ;
LIST Infinitive = (inf) ;
LIST VerbInf = (vblex inf) ;
LIST VerbSpecAux = (vaux) ;
LIST VerbAux = (vaux) ;
LIST VerbSupine = (vblex supn) ;
LIST VerbBe = (vbser) ;
LIST Verb = (vblex) (vbser) (vbhaver);
LIST FiniteTenses = (psi) (pss) (pri) (prs) (past);
LIST Imp = (imp) ;
LIST PastPart = (pp) ;
LIST RelativeConjunction = (rel) ;
LIST Conjunction = (cnjcoo) (cnjadv) (cnjsub) ;
LIST CS = (cnjsub);
LIST CC = (cnjcoo);
LIST Num = (num) ;
LIST Exclamation = (ij) ;
LIST Def = (def) ;
LIST Det = (det) ;
LIST Interj = (ij) ;

LIST Fem = (f);
LIST Msc = (m);
LIST Neu = (nt);

LIST Nom = (nom);
LIST Gen = (gen);
LIST Acc = (acc);
LIST Dat = (dat);

LIST Inf = (inf);

LIST GenPrep = "án"ri "auk"ri "austan"ri "innan"ri "í staö"ri "meðal"ri "megin"ri "milli"ri "millum"ri 
               "neðan"ri "norðan"ri "ofan"ri "sakir"ri "sunnan"ri "sökum"ri "til"ri "utan"ri "vegna"ri "vestan"ri ;
LIST AccPrep = "fyrir ofan"ri "gegnum"ri "kringum"ri "um"ri "umfram"ri "umhverfis"ri ;
LIST DatPrep = "að"ri "af"ri "andspænis"ri "ásamt"ri "frá"ri "gagn"ri "gagnvart"ri "gegnt"ri "handa"ri 
               "hjá"ri "meðfram"ri "mót"ri "móti"ri "nálægt"ri "undan"ri "úr"ri ;
LIST AccDatPrep = "á"ri "eftir"ri "fyrir"ri "í"ri "með"ri "undir"ri "við"ri "yfir"ri ;

SET VFIN = Verb + FiniteTenses;

LIST NAGD = nom acc gen dat;
LIST GENDER = m f nt;
LIST NUMBER = sg pl;

LIST COMMA = (",") ;


SET NomMscFem = (n m) OR (n f) ;
SET NomMscNeu = (n m) OR (n nt) ;
SET NomFemNeu = (n f) OR (n nt) ;

SET NOTDAT = Nom | Gen | Acc ;
SET NOTACC = Nom | Gen | Dat ;
SET NOTACCDAT = Nom | Gen ;
SET OBL = Acc | Dat | Gen ;

SET PRE-N =  Adj | Det | (N Gen) | Num | (prn gen) | (prn dem) | CC ; # Det???

SET WORD = N | Verb | Adj | Prep | Prn | Det | Adv | CC | CS | Interj | Num | ("\?") ;
SET MARK =  COMMA | ("\") | ("\;") ; #"
SET WORDMARK = WORD | MARK ;

SET CLB = MARK | CC | CS;

SET NPNHA = WORDMARK - PRE-N - Adv ;


SECTION

# Specific lexemes

REMOVE N IF (-1C N OR ProperN) (0 ("<á>"ri));


#################################################################################
# Proper nouns
#################################################################################

# hús Láru

SELECT ProperN IF (-1C N) (1 Gen) ; 					# !tag.isCase(GENITIVE) ;
SELECT ProperN IF (-1C GenPrep) (1 Gen) ;	
SELECT ProperN IF (-1C AccPrep) (1 Acc) ;
SELECT ProperN IF (-1C DatPrep) (1 Dat) ;

# Hildar Guðmundsdóttur
SELECT ProperN IF (-1C ProperN) (-1C Gen) (0 ("<.*dóttur">)) (0 Gen) ; 	# !tag.isCase(GENITIVE) ;
SELECT ProperN IF (-1C ProperN) (-1C Fem) ; 				# !tag.isGender(FEMININE) ;
SELECT ProperN IF (-1C ProperN) (-1C Msc) ; 				# !tag.isGender(MASCULINE) ;

#################################################################################
# Ns
#################################################################################

# hann sýnir but stakk hún hendi is ok, það sýnir aldrei

# CEx.  Ég borða fisk
#SELECT N IF ((-1C PrnPers) OR (-1C PrnPers LINK -1C PrnDem) OR (-1C ("<við>"ri))); 

SELECT N IF (-1C GenPrep) (1 Gen) ;	
SELECT N IF (-1C AccPrep) (1 Acc) ;
SELECT N IF (-1C DatPrep) (1 Dat) ;

# að (cn/aa) beina (sgn/n*) mér
SELECT N IF (-1 Inf) (0 VerbInf) ;

# einhvern sem sæti
SELECT N IF (-1 ("<sem>"ri)) (0 Verb) ;

# choose the verb
SELECT N IF (-1C ProperN) (0 Verb) ;

# Disallow two nouns in a row unless the latter is in the genitive case
SELECT N IF (-1C N) (NOT -1 Gen) ((NOT -2 ProperN) OR (-2 BOS));

# select the possessive pronoun, "pabbi sinn"
SELECT PrnPoss IF (-1C N);

SELECT ("á"ri) IF (-1 Adv);

# skal (ekki) hita
SELECT VerbInf IF ((-1 VerbSpecAux) OR (-1C Adv LINK NOT -2 BOS LINK -2 VerbSpecAux));

# hafði talið, velja sagnbót
SELECT VerbSupine IF (-1 VerbAux);

# select the adjective, but ok is "fyrir framan X", and ok is "bara verð markaðarins"
SELECT Adv IF (0 Adj) (-1 BOS OR (NOT -1 Prep)) (1 EOS OR (NOT 1 N LINK 1 Gen));

# sú líkn
# CEx. Guð sá að ljósið var gott
#SELECT PrnDem IF (-1 N) (NOT 0 N);

SELECT VerbBe IF (NOT 0 N);

# select the adjective, "hann er lasinn",
SELECT Adj IF (-1 VerbBe) (NOT -2 BOS) (-2 PrnPers);


#################################################################################
# Adjectives
#################################################################################

# Check this.
# Ex. Gud gjörði tvö stóru ljósin.
REMOVE Weak IF (1C N) (NOT 1 Def);


#################################################################################
# Adverbs
#################################################################################



#################################################################################
# Personal pronouns 
#################################################################################


#################################################################################
# Indefinite pronouns 
#################################################################################


#################################################################################
# Demonstrative pronouns 
#################################################################################

# Ex. Guð talaði öll þessi orð og sagði:
REMOVE Fem IF (0C PrnDem)  (1C Neu);
REMOVE Fem IF (0C PrnDem)  (1C Msc);
REMOVE Msc IF (0C PrnDem)  (1C Neu);
REMOVE Msc IF (0C PrnDem)  (1C Fem);
REMOVE Neu IF (0C PrnDem)  (1C Fem);
REMOVE Neu IF (0C PrnDem)  (1C Msc);
REMOVE Msc IF (0C PrnDem)  (1C Fem);


#################################################################################
# Reflexive pronouns 
#################################################################################


#################################################################################
# Interrogative pronouns 
#################################################################################


#################################################################################
# Possessive pronouns 
#################################################################################


#################################################################################
# Prepositions
#################################################################################


#################################################################################
# Verbs  
#################################################################################

SELECT (vblex p3 sg) IF (-1C (prn p3 sg));
# Ex.: Ég borða fisk
SELECT (vblex p1 sg) IF (-1C (prn p1 sg));

SELECT (vbser p3 sg) IF (-1 (prn p3 sg));
SELECT (vbser p1 sg) IF (-1 (prn p1 sg));

SELECT (vaux p3 sg) IF (-1 (prn p3 sg));
SELECT (vaux p1 sg) IF (-1 (prn p1 sg));
# Ex. Við munum fara í dag
SELECT (vaux p1 pl) IF (-1 (prn p1 pl));

# Ex.: Þið lesið bókina
REMOVE PastPart IF (-1C PrnPers);

# Ex.: Hann sagði að skipið færi á morgun
REMOVE Verb IF (-1 CS) (1 VFIN);

# Ex.: Peir hafa ekki vín
SELECT Verb IF (-1 (prn p1 sg)) (0 (p1 sg)) (1C Adv);
SELECT Verb IF (-1 (prn p2 sg)) (0 (p2 sg)) (1C Adv);
SELECT Verb IF (-1 (prn p3 sg)) (0 (p3 sg)) (1C Adv);
SELECT Verb IF (-1 (prn p1 pl)) (0 (p1 pl)) (1C Adv);
SELECT Verb IF (-1 (prn p2 pl)) (0 (p2 pl)) (1C Adv);
SELECT Verb IF (-1 (prn p3 pl)) (0 (p3 pl)) (1C Adv);

REMOVE:Imp_with_subj Imp IF (-1C PrnPers);

# Ex. Sá einn, sem er frá Guði, hefur séð föðurinn.
REMOVE:AlwaysVfin VFIN IF (1C* VFIN BARRIER CLB);

REMOVE:AlwaysVfin VFIN IF (-1C* VFIN BARRIER CLB);


#################################################################################
# NPs  
#################################################################################

REMOVE:ufc_f  Adj + Fem IF (0  $$NAGD) (*1 NomMscNeu + $$NAGD BARRIER NPNHA LINK NOT 0 Fem);
REMOVE:ufc_m  Adj + Msc IF (0  $$NAGD) (*1 NomFemNeu + $$NAGD BARRIER NPNHA LINK NOT 0 Msc);
REMOVE:ufc_nt Adj + Neu IF (0  $$NAGD) (*1 NomMscFem + $$NAGD BARRIER NPNHA LINK NOT 0 Neu);

SELECT:r28 $$GENDER IF (-1C (Adj) + $$GENDER)(0 N);
SELECT:r30b $$GENDER IF (-1C (Det) + $$GENDER)(0 N);
SELECT:r30c $$GENDER IF (-1C (Pron Indef) + $$GENDER)(0 N);

SELECT:r29 $$NAGD IF (-1C (Adj) + $$NAGD)(0 N);
SELECT:r30b $$NAGD IF (-1C (Det) + $$NAGD)(0 N);
SELECT:r30c $$NAGD IF (-1C (Pron Indef) + $$NAGD)(0 N);

SELECT:r30 $$NUMBER IF (-1C (Adj) + $$NUMBER)(0 N);
SELECT:r30b $$NUMBER IF (-1C (Det) + $$NUMBER)(0 N);
SELECT:r30c $$NUMBER IF (-1C (Pron Indef) + $$NUMBER)(0 N);


#Ex. við eiginkonu sína
SELECT $$GENDER IF (-1C (n) + $$GENDER) (0 PrnPoss) (0 ("sinn"ri));


